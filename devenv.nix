{ lib, pkgs, ... }: {
  languages.rust = {
    enable = true;
    # The Rust version is pinned in rust-toolchain.toml, which the native CI
    # runners (artifact workflows that cannot use devenv) read via rustup.
    # The musl targets for the fully-static Go binary are declared in
    # rust-toolchain.toml (read automatically via toolchainFile).
    toolchainFile = ./rust-toolchain.toml;
  };

  languages.javascript = {
    directory = "./docs";
    enable = true;
    # Node 22 (the plain nixpkgs default) bundles npm 10.x, which mishandles
    # npm Trusted Publishing's OIDC handshake and can even misreport a brand
    # new package's first publish as a 404. Node 24 bundles npm >= 11.5.1.
    package = pkgs.nodejs_24;
    npm = {
      enable = true;
      install.enable = true;
    };
  };
  # Python is used by the reference SDK (secretspec-py), a pyo3 extension
  # (secretspec-py-native) that statically links the resolver in directly.
  languages.python = {
    enable = true;
    venv = {
      enable = true;
      requirements = ''
        maturin
        pytest
      '';
    };
  };
  # Go SDK (secretspec-go): default binding is purego (dlopen, no cgo); the
  # `-tags static` binding uses cgo to statically link libsecretspec_ffi.a, and on
  # Linux is built fully static against musl (see the env block below).
  languages.go.enable = true;
  # Ruby SDK (secretspec-rb) compiles an mkmf C extension that statically links
  # libsecretspec_ffi.a.
  languages.ruby.enable = true;
  # Haskell SDK (secretspec-hs) links the C ABI at build time via the FFI.
  # Supply its only non-boot dependency from Nix's binary cache. Otherwise a
  # cold Cabal store compiles aeson and roughly forty transitive packages from
  # source in every hosted SDK job.
  languages.haskell = {
    enable = true;
    package = pkgs.haskellPackages.ghcWithPackages (hpkgs: [ hpkgs.aeson ]);
  };
  # C# SDK (secretspec-dotnet) loads the C ABI through P/Invoke. The NuGet
  # package carries runtime-specific cdylibs; local tests use
  # SECRETSPEC_FFI_LIB from scripts/ci-sdks.sh.
  languages.dotnet.enable = true;
  # PHP SDK (secretspec-php) has two native backends over the same resolver:
  #   * secretspec-php-native, an ext-php-rs extension that embeds the resolver
  #     (the production path: no ffi.enable, works in FPM like ext-redis); and
  #   * a runtime ext-ffi fallback that dlopens the secretspec-ffi cdylib.
  # The pure-PHP client prefers the extension when loaded. ext-ffi (enabled here)
  # covers the fallback + dev; composer (bundled with languages.php) manages the
  # dev-only phpunit dependency.
  languages.php = {
    enable = true;
    extensions = [ "ffi" ];
    ini = ''
      ffi.enable = true;
    '';
  };

  packages = [
    # documentation link validation
    pkgs.lychee
    # coverage testing
    pkgs.cargo-tarpaulin
    # installers
    pkgs.cargo-dist
    # bitwarden-cli for integration testing
    pkgs.bitwarden-cli
    # docker CLI for tests/vaultwarden_harness.sh, which runs the disposable
    # Vaultwarden + TLS proxy containers. Client only: the harness talks to
    # whatever runtime the developer already provides (Docker Desktop, colima,
    # or a podman machine exposing /var/run/docker.sock).
    # docker-client currently aliases the insecure Docker 28 package.
    (pkgs.docker_29.override { clientOnly = true; })
    # Building the secretspec-php-native extension (ext-php-rs) needs php-config +
    # the PHP dev headers, and bindgenHook wires libclang/clang system headers so
    # ext-php-rs's bindgen step can parse php.h.
    pkgs.php.unwrapped.dev
    pkgs.rustPlatform.bindgenHook
    # For development of the SOPS provider
    pkgs.sops
    pkgs.pkg-config
    # Installs the secretspec-ffi archive with its header and pkg-config file
    pkgs.cargo-c
  ];

  # Fully-static musl build of the Go SDK (-tags static + -extldflags -static).
  # Keep these Linux-only: interpolating the cross-toolchain paths on macOS makes
  # Nix build a Linux-targeting GCC toolchain from source just to enter the shell.
  # The musl C cross-toolchain and static libunwind are referenced HERE by
  # absolute path only -- NOT added to `packages`, because devenv `packages` inject
  # their lib dirs into the host NIX_LDFLAGS. Referenced by path, libunwind is
  # realised into the store without polluting the host build environment. The
  # CC_/linker vars are musl-target-scoped, so host (glibc) cargo builds are
  # unaffected; MUSL_CC / MUSL_STATIC_LDFLAGS feed the cgo link step.
  env = lib.optionalAttrs pkgs.stdenv.isLinux (
    let
      muslcc = "${pkgs.pkgsCross.musl64.stdenv.cc}/bin/x86_64-unknown-linux-musl-gcc";
    in
    {
      CC_x86_64_unknown_linux_musl = muslcc;
      CARGO_TARGET_X86_64_UNKNOWN_LINUX_MUSL_LINKER = muslcc;
      MUSL_CC = muslcc;
      MUSL_STATIC_LDFLAGS = "-L${pkgs.pkgsStatic.libunwind}/lib";
    }
  );

  git-hooks.hooks = {
    rustfmt.enable = true;
    clippy.enable = true;
    # TODO: this should be done by devenv
    clippy.settings.offline = false;
  };

  # sudo-secretspec-cli is a macOS-only downstream companion and does not build
  # on Linux; run its suite with `cargo test -p sudo-secretspec-cli` on macOS.
  enterTest = ''
    cargo test --all --exclude sudo-secretspec-cli
  '';

  scripts.test-cli-integration.exec = ''
    # Build the CLI for integration tests
    cargo build --release
    export PATH="$PWD/target/release:$PATH"

    # Run CLI integration tests
    bash tests/cli-integration.sh
  '';

  processes.docs.exec = ''
    cd docs && npm run dev
  '';
}
